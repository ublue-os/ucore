set dotenv-load := true

CI_BUILD_TAG := env("CI_BUILD_TAG", "")
COREOS_VERSION := env("COREOS_VERSION", "stable")
FEDORA_VERSION := env("FEDORA_VERSION", "43")
IMAGE_REGISTRY := env("IMAGE_REGISTRY", "ghcr.io/ublue-os")
KERNEL_FLAVOR := env("KERNEL_FLAVOR", if COREOS_VERSION == 'stable' { "longterm-6.12" } else { 'coreos-' + COREOS_VERSION })
NVIDIA_TAG := env("NVIDIA_TAG", "")
SELINUX := path_exists('/sys/fs/selinux')
TARGET_IMAGE := env("TARGET_IMAGE", "ucore-minimal")

_default:
    @just --list --unsorted

upstream-version image:
    #!/usr/bin/env bash
    version=$(skopeo inspect docker://"{{ image }}":{{ COREOS_VERSION }} | \
            jq -r '.["Labels"]["org.opencontainers.image.version"]')
    if [ -z "$version" ] || [ "null" = "$version" ]; then
      echo "inspected image ({{ image }}) version must not be empty or null" >&2
      exit 1
    fi
    echo "$version"

upstream-image:
    #!/usr/bin/env bash
    image='quay.io/fedora/fedora-coreos'
    version=$(just upstream-version $image)
    echo "$image:$version"

containerfile upstream_image='':
    #!/usr/bin/env bash
    set -x
    declare -a CPP_FLAGS
    CPP_FLAGS+=(
      "-DAKMODS_TAG={{ KERNEL_FLAVOR }}-{{ FEDORA_VERSION }}"
      "-DCOREOS={{ COREOS_VERSION }}"
      "-DIMAGE_REGISTRY={{ IMAGE_REGISTRY }}"
      "-DKERNEL={{ KERNEL_FLAVOR }}"
      "-DUPSTREAM_IMAGE={{ if upstream_image == '' { shell('just upstream-image') } else { upstream_image } }}"
    )
    if [ -n "$NVIDIA_TAG" ]; then
      # NVIDIA_TAG=={{ NVIDIA_TAG }}
      CPP_FLAGS+=(
        "-DNVIDIA=${NVIDIA_TAG#-}"
      )
    fi

    # direct cpp joins lines and does not work as expected
    cpp -E -P "${CPP_FLAGS[@]}" Containerfile.in -o Containerfile
    cat Containerfile

build upstream_image='':
    #!/usr/bin/env bash
    set -x
    just containerfile {{ upstream_image }}
    # TODO: use LABELS_FILE to include labels in image
    BUILD_TAG={{ CI_BUILD_TAG }}
    if [ -z "$BUILD_TAG" ]; then
        BUILD_TAG={{ COREOS_VERSION }}{{ NVIDIA_TAG }}
    fi
    podman build -f Containerfile \
        --target {{ TARGET_IMAGE }} \
        -t "{{ TARGET_IMAGE }}:${BUILD_TAG}" .
